ERC777-K
=======

Below we describe ERC777-K, our formal specification of a refined variant
of the ERC777 standard token in K.

Our objective is to define ERC777-K in such a way that it can be imported
by an arbitrary programming language semantics, whose programs can then make
use of the ERC777-K functions.
Our immediate reason for doing so is to test ERC777-K programmatically.
But this design choice can have other advantages, too.
For example, it can be seen as a way to *plug-and-play* ERC777-K support
to your favorite language, provided you have a K semantics for it.

As is customary for a K definition, we divide it into 2 modules: 
`ERC777-SYNTAX` for the syntax and `ERC777` for the actual evaluation rules.  

## 1 Syntax

We start by defining the module `ERC777-SYNTAX`. It contains only the 
syntactic definitions for the entities defined in ERC777 standard 
`ERC777-SYNTAX`, such as  functions and events. In addition here we define the 
sorts and the relationship between them. Additional, auxiliary syntactic 
definitions are present in module `ERC777`.
```
module ERC777-SYNTAX
  imports DOMAINS
```

### 1.1 Syntactic sorts

ERC777-K refers to 3 major data types: `uint256`, `address` and `bytes`.
Type `uint256` is used for token values, and corresponds to `ValueType` in ERC777-K.
(We use `ValueType` here to avoid name collision with variables called `Value`.)
Type `address` corresponds to `Address` here. These 2 types are natural numbers, 
so we model them as K unbound integer - `Int`. The 3rd essential type, `bytes` 
is essentially used to pass messages across various operations. It corresponds 
to `Data` in ERC777-K, modeled as `String`. 
```
  syntax Address ::= Int
  syntax ValueType ::= Int
  syntax Data ::= String
```

In addition ERC777 mentions the type `string` which is modeled by K String,
and `bool` modeled by K `Bool`.

Next we define expression sorts that are later used to define ERC777 functions. 
All numeric expressions are captured by `AExp`. It thus includes both `Address` 
and `ValueType`. `BExp` captures boolean expressions.
The auxiliary construct `#void` represents the return value of functions that
don't have a return type. It allows a more uniform semantics for functions.
In addition, we need the base expression type `Exp` which captures all possible 
expressions, including those not captured by more particular sorts.
(`SetExp` will be described later).
```
  syntax AExp  ::= ValueType | Address
  syntax BExp  ::= Bool

  syntax Void ::= "#void"
  
  syntax Exp ::= AExp | BExp | SetExp | Data | Void
```

### 1.2 Main Functions
Next we define ERC777 functions.

The 2 functions below are constant, returning `String`:
```
  syntax Exp ::= name ( )
               | symbol ( )
```
Since we ERC777 doesn't 
define any operations over strings, we don't need a separate sort for string
expressions. Thus we subsort them directly to `Exp`.

Here are remaining functions from the standard:
```
  syntax AExp ::= totalSupply ( )
                | balanceOf ( AExp ) [strict]
                | granularity ( )

  syntax SetExp ::= defaultOperators ( )
                  | Set

  syntax BExp ::= isOperatorFor ( AExp , AExp ) [strict]

  syntax Exp ::=  authorizeOperator ( AExp )                        [strict]
                | revokeOperator ( AExp )                           [strict]
                | send ( AExp , AExp , Data )                       [strict]
                | operatorSend ( AExp , AExp , AExp , Data , Data ) [strict]
                | burn ( AExp )                                     [strict]
                | operatorBurn ( AExp , AExp , Data )               [strict]
                 | "revert"
```
We use K strictness mechanism to be able to use these functions in programs,
with expressions passed as arguments.
The function `defaultOperators` returns an `address[]` in the standard. Here we 
use the sort `SetExp` for the function, and generic `Set` for the actual array 
of addresses. All functions that don't return a value are subsorted directly 
to `Exp`.
The construct `revert` above represents the function execution result `revert`
mentioned in the standard.

In addition, we define one more function that is not part of the standard:
```  
  syntax Exp ::=  operatorMint ( AExp , //from
                                 AExp , //amount
                                 Data , //data
                                 Data   //operatorData.
                               ) [strict]
```
ERC-777 does not define a specific function for minting, but it allows the minting
operation through additional functions and defines a set of rules that must be 
followed when minting tokens. To capture these rules, we define the function 
`operatorMint` with a semantics symmetrical to `operatorBurn`.

### 1.3 Events

The ERC20 functions are required, at success, to log the following of events:
```
  syntax Event ::= Sent ( Address , Address , Address , ValueType , Data , Data )
                 | Minted ( Address , Address , ValueType , Data , Data )
                 | Burned ( Address , Address , ValueType , Data )
                 | AuthorizedOperator ( Address , Address )
                 | RevokedOperator ( Address , Address )
```

The events are then collected in a log.
The precise syntax of the log is not specified in the ERC20 standard, so
in ERC20-K we take the freedom to choose the syntax
`Events: <event1> <event2> ... <eventn>`:

```
  syntax EventLog ::= "Events:" | EventLog Event
```

The syntax of the event log is unconstrained (by semantic rules) and
is irrelevant for implementations.
All the above is saying is that implementations of ERC20-K must implement
the notion of an event log, together with an *empty* event log and the
capability to *append* a new event to an existing event log.

### 1.4 Hooks

The main addition of ERC-777 over ERC-20 are 2 function "hooks":
`tokensToSend` and `tokensReceived` These are function interfaces 
which can be registered by an address, and will be called whenever 
an operation that removes tokens, or adds tokens to the respective address 
is invoked.

Below we define the 2 hooks:
```
  syntax Stmt ::= AExp "." "tokensToSend" "(" AExp "," AExp "," AExp "," AExp "," Data "," Data ")" [strict]

  syntax Stmt ::= AExp "." "tokensReceived" "(" AExp "," AExp "," AExp "," AExp "," Data "," Data ")" [strict]
```
Here the qualifier (before ".") represents the hook address, and the function 
arguments are the same as in the standard.

### 1.5 ERC20-compatibility
ERC777 defines additional rules that must be preserved when an ERC777 token
contract is compatible with ERC20. There are additional rules both for ERC777
functions and for ERC20. To capture them, we decided to fully model
ERC777+ERC20 in ERC777-K. Thus, we also define all ERC20 functions and events:
```
  syntax AExp ::= allowance ( AExp , AExp ) [strict]
                | decimals ( )

  syntax BExp ::= approve ( AExp , AExp )             [strict]
                | transfer ( AExp , AExp )            [strict]
                | transferFrom ( AExp , AExp , AExp ) [strict]

  syntax Event ::= Transfer ( Address , Address , ValueType)
                 | Approval ( Address , Address , ValueType)

```

This completes the syntax module.
```
endmodule
```

## 2 Configuration

The second module contains the configuration and the actual execution rules.

```
require "erc777-syntax.k"

module ERC777

  imports ERC777-SYNTAX
  imports INT
  imports BOOL
  imports STRING
  imports MAP
  imports SET
  imports LIST
  imports KCELLS
```

Next we present the configuration and the meaning of each cell:
```
  configuration
     <ERC777>
       <caller> 0:Address </caller>
       <k> $PGM:K </k>
       <accounts>
         <account multiplicity="*">
           <id> 0:Address </id>
           <balance> 0:ValueType </balance>

           <operators> .Set </operators>
           <senderHook> 0:Address </senderHook>
           <receiverHook> 0:Address </receiverHook>
           <isContract> .K </isContract>
           <allowances>
             <allow multiplicity="*">
               <spender> 0:Address </spender>
               <amount> 0:ValueType </amount>
             </allow>
           </allowances>
         </account>
       </accounts>
       <supply> 0:ValueType </supply>
       <nameC> "" </nameC>
       <symbolC> "" </symbolC>
       <granularityC> 1 </granularityC>
       <defaultOperatorsC> .Set </defaultOperatorsC>
       <ERC20CompatibleC> true </ERC20CompatibleC>
       <acceptRegularNoReceiverHook> true </acceptRegularNoReceiverHook>
       <burnAllowed> true </burnAllowed>
       <log> Events: </log> // event*
       <atomic> .K </atomic>
     </ERC777>
```
- `<caller>` - the caller address
- `<k>` - computation cell
- `<account>` - all information relevant to one account
  - `<id>` - account address, key to the account
  - `<balance>` - token balance
  - `<operators>` - the set of operators for this account. Initialized with the default operators.
  - `<senderHook>` - address of the sender hook registered for this account, or 0 if there is no hook.
  - `<receiverHook>` - address of the receiver hook registered for this account, or 0 if there is no hook.
  - `<isContract>` - whether this account is a contract or a regular account.
  - `<allowances>` ERC-20 allowances associated with this account. Note that they are 
  completely separated from ERC-777 `<operators>`, as required by the standard.
  Is initialized with an allowance entry for any account in the configuration,
  with allowance 0.
    - `<spender>` - spender account for this particular account.
    - `<amount>` - allowance amount for this <account, spender> pair.
- `<supply>` - total supply in this contract
- `<nameC>` - token name, returned by `name()`. Calling it `nameC` was required to avoid name 
collision with function `name()`.
- `<symbolC>` - symbol of the token, returned by `symbol()`
- `<granularityC>` - contract granularity
- `<defaultOperatorsC>` - set of default operators. Used to initialize `<operators>`
when a new account is created
- `<ERC20CompatibleC>` - Contract parameter. Whether the contract is ERC-20 compatible. Affects the types of logs created.
- `<acceptRegularNoReceiverHook>` - Contract parameter. If true, regular addresses with 
no receiver hook can receive tokens through send or mint operations. If false, such operations 
will result in revert.
- `<burnAllowed>` - Cotnract parameter. If true, burn operations are allowed. Otherwise they result 
in revert.
- `<log>` - events log
- `<atomic>` - backup cell that stores old values of certain cells at the beginning of an 
    ERC-777 function. Used to implement `revert` operation.
    
For the remainder of the semantics, we assume the configuration is already
*correctly* initialized: all the account `<id/>`s are distinct, their
`<balance/>`s are non-negative, and the total `<supply/>` is the sum of all
the balances) and the `<caller/>` is known.
In our simple IMP programming language that we define on top of the ERC20-K
semantics (in `erc777-imp.k`) we define macros that can be used to
initialize the configuration.

## 3 Semantics

ERC777 was originally designed for the EVM, whose integers
(of type `uint256`) are unsigned and have `256` bits, so can go up
to `2^256`.
The exact size of integers is important for arithmetic overflow,
which needs to be rigorously defined in the formal specification.
However, the precise size of integers can be a parameter of the
ERC777-K specification.
This way, the specification can easily be used with various execution
environments, such as [eWASM](https://github.com/ewasm/) or 
[IELE](https://github.com/runtimeverification/iele-semantics), or
with various programming languages
(e.g., [Viper](https://github.com/ethereum/viper) appears to converge
towards integers up to `2^128`).
The syntax declaration and the rule below define an integer
constant `#MAXVALUE` and initialize it with the largest integer
on 256 bits (one can set it to "infinity" if one does not care about
overflows or if one's computational infrastructure supports unbounded
integers, like
[IELE](https://github.com/runtimeverification/iele-semantics) does):

```
  syntax Int ::= "#MAXVALUE"  [function]
  rule #MAXVALUE => 2 ^Int 256 -Int 1
```

### 3.1 Constant functions
```
  rule <k> name ( ) => NAME ...</k>
       <nameC> NAME </nameC>

  rule <k> symbol ( ) => SYMBOL ...</k>
       <symbolC> SYMBOL </symbolC>

  rule <k> granularity ( ) => G ...</k>
       <granularityC> G </granularityC>

  rule <k> defaultOperators ( ) => DefOps ...</k>
       <defaultOperatorsC> DefOps </defaultOperatorsC>
```

The rules above define the semantics for 4 constant functions, simply by reading the
value from corresponding configuration cell.

### 3.2 totalSupply

```
  rule <k> totalSupply ( ) => TotalSupply ... </k>
       <supply> TotalSupply </supply>
```
Similar to above, reading the value from `<supply>`.

### 3.3 balanceOf

```
  rule <k> balanceOf ( Holder:Int  ) => Balance ... </k>
       <id> Holder </id>
       <balance> Balance </balance>
    requires Holder =/=Int 0

  //Required for send/operatorSend revert cases.
  rule balanceOf(0) => 0
```
For `balanceOf` we need 2 rules. First rule represents the regular case. It rewrites 
into the `<balance>` value associated with the given parameter.
The second rule is required for the case when parameter `Holder` is `0`.
Since `0` is an invalid address, we don't have an `<account>` cell for it,
thus the semantics for this case have to be defined through a separate rule.

### 3.4 isOperatorFor
Similar to `balanceOf`

```
  rule <k> isOperatorFor ( Operator:Int , Holder:Int )
        => Operator in Operators ... </k>
       <id> Holder </id>
       <operators> Operators:Set </operators>
    requires Holder =/=Int 0

  rule isOperatorFor(_, 0) => false
```

### 3.5 authorizeOperator
```
  rule <k> authorizeOperator ( Operator:Int ) => #void ... </k>
       <caller> Holder </caller>
       <id> Holder </id>
       <operators> Operators => Operators SetItem(Operator) </operators>
       <log> Log => Log AuthorizedOperator ( Operator , Holder ) </log>
       requires Holder =/=Int Operator andBool Operator =/=Int 0

  rule <k> authorizeOperator ( Operator:Int ) => revert ... </k>
       <caller> Holder </caller>
       requires Holder ==Int Operator orBool Operator ==Int 0
```
The first rule corresponds to the valid call of `authorizeOperator`: caller 
different from argument and argument `!= 0`. ERC777 states that an account (caller)
cannot authorize or revoke authorization from itself. 
The rule adds the argument to the list of operators of the caller, and generates
the log event `AuthorizedOperator`.

The 2nd rule represents the invalid call, all other cases, which generates a `revert`.

The `revert` construct has no other semantics in this module, but is printed
to the output for debug purposes, in `ERC777-IMP` module. 

### 3.6 revokeOperator
Requires similar 2 rules. Successful operation generates the log event 
`RevokedOperator`.
```
  rule <k> revokeOperator ( Operator:Int ) => #void ... </k>
       <caller> Holder </caller>
       <id> Holder </id>
       <operators> Operators => Operators -Set SetItem(Operator) </operators>
       <log> Log => Log RevokedOperator ( Operator , Holder ) </log>
       requires Holder =/=Int Operator andBool Operator =/=Int 0

  rule <k> revokeOperator ( Operator:Int ) => revert ... </k>
       <caller> Holder </caller>
       requires Holder ==Int Operator orBool Operator ==Int 0
```
### 3.7 send and operatorSend

#### 3.7.1 send
Send function is simply an alias to `operatosSend` with first argument = caller:
```
  rule <k>         send (          To:Int , Value:Int , Data      )
        => operatorSend ( Holder , To     , Value     , Data , "" ) ... </k>
       <caller> Holder </caller>
```

#### 3.7.2 operatorSend - initial rules
Function `operatorSend` requires many steps defined through auxiliary constructs.
All together they represent the bulk of the semantics. We start with 2 initial rules:
```
  rule <k> operatorSend ( 0 => Caller , _,_,_,_ ) ... </k>
       <caller> Caller </caller>
    requires Caller =/=Int 0 //just for safety

  rule operatorSend ( From , To , Value , Data , OperatorData )
       => #operatorSendAux ( From , To , Value , Data , OperatorData , balanceOf(From), balanceOf(To) )
    requires From =/=Int 0 //just for safety
```
The first rule deals with the case when first argument (`From`) is `0`. According to ERC777,
in this case from value should be Teh caller.
The 2nd rule rewrites operatorSend into the auxiliary construct `#operatorSendAux`
and uses strictness mechanism to compute a few more values: the balance of `From` account
and balance of `To`.

#### 3.7.3 #operatorSendAux
```
  syntax K ::= "#operatorSendAux" "(" Address "," Address "," ValueType "," Data "," Data "," AExp "," AExp ")" [seqstrict(6,7)]

  rule <k> #operatorSendAux ( From , To , Value , Data , OperatorData , BalanceFrom:Int, BalanceTo:Int )
        => #atomicBegin
        ~> #ensure( #and(
              From =/=Int 0,
              To =/=Int 0,
              isOperatorFor ( Operator , From ),
              Value >=Int 0,
              BalanceFrom >=Int Value,
              #isMultipleOf ( Value , granularity() ) ))
        ~> #callSender ( Operator , From , To , Value , Data , OperatorData )
        ~> #transfer ( From , To , Value )
        ~> #callRecipient ( Operator , From , To , Value , Data , OperatorData , false )
        ~> #log( Sent(Operator , From , To , Value , Data , OperatorData) )
        ~> #logERC20( Transfer(From , To , Value) )
        ~> #atomicEnd(#void) ... </k>
       <caller> Operator </caller>
```
Here the bulk of the logic of `operatorSend` happens. The `#operatorSendAux` is rewritten
into a sequence of operations, each carrying a specific step defined in ERC777.
The first construct `#atomicBegin`, saves the relevant part of the configuration
into the cell `<atomic>`. It is then later used to revert all changes in case the operation
ends in `revert`.

The `#ensure` clause checks all the preconditions that must be true in order to operation to 
succeed. If argument expression evaluates to false, the operation ends in `revert`.
Note that overflow is not checked here, because new balance is guaranteed to be 
`<= totalSupply`, and thus cannot overflow.

The `#callSender` calls the sender hook for `From` address, if it exists.
The `#transfer` performs the actual balance transfer.
Next `#callRecipient` calls the receiver hook for `To` address, if exists.
Because the call is performed after balance transfer, inside the receiver hook new balances 
will be visible.

The `#log` construct will log the ERC777-specific event `Send`. Then the `#logERC20`
construct will log the ERC20-specific `Transfer`, only when ERC20 compatibility is enabled.

The last construct, `#atomicEnd`, deletes the backup data created by `#atomicEnd`
if operation completed successfully. The argument of `#atomicEnd` is required
to correctly model function return value. We will return to it later.

#### 3.7.4 #callSender

There are 2 rules for `#callSender`, first for the case when Hook is registered 
(e.g. `Hook != 0`) and 2nd for the case when it's not registered: 
```
  syntax Stmt ::= #callSender ( Address , // operator
                                Address , // from
                                Address , // to
                                ValueType ,  // amount
                                Data ,    // userData
                                Data      // operatorData
                              )

  rule <k> #callSender ( Operator , From , To , Value , Data , OperatorData )
        => Hook . tokensToSend ( Operator , From , To , Value , Data , OperatorData ) ... </k>
       <id> From </id>
       <senderHook> Hook </senderHook>
       requires Hook =/=Int 0

  rule <k> #callSender ( Operator , From , To , Value , Data , OperatorData ) => . ... </k>
       <id> From </id>
       <senderHook> 0 </senderHook>
```

When a hook is registered, `#callSender` is rewritten into `tokensToSend` qualified by the
hook registration address. ERC777-K does not define any implementation for `tokensToSend`.
In the extension module `ERC777-IMP` we have 2 implementation for testing purposes:
one that accepts the operation and another that reverts. We didn't
implement a more general-purpose implementation because behavior of the hook
is not part of the ERC777 standard.

If the hook produces `revert`, it will be propagated by rules that are presented later
outside `operationSent`.

The 2nd rule, for the case when hook is not registered, simply dissolves `#callSender`.

#### 3.7.5 #callRecipient

There are 2 rules here, similar to the rules for `#callSender`:
```
  syntax Stmt ::= #callRecipient ( Address , // operator
                                   Address , // from
                                   Address , // to
                                   ValueType ,  // amount
                                   Data ,    // userData
                                   Data ,    // operatorData
                                   Bool      // fromERC20: called by ERC20 functions
                                 )

  // If the recipient is a contract, which has not registered an ERC777TokensRecipient implementation; the token contract:
  // * MUST revert if the tokensReceived hook is called from a mint or send call.
  // * SHOULD accept if the tokensReceived hook is called from an ERC20 transfer or transferFrom call.
  rule <k> #callRecipient ( Operator , From , To , Value , Data , OperatorData , _ )
        => Hook . tokensReceived ( Operator , From , To , Value , Data , OperatorData ) ... </k>
       <id> To </id>
       <receiverHook> Hook </receiverHook>
       requires Hook =/=Int 0

  rule <k> #callRecipient ( Operator , From , To , Value , Data , OperatorData , FromERC20 )
        => #ite( #or(FromERC20, #and(AcceptRegular, notBool ISCONTRACT) ) , . , revert ) ... </k>
       <id> To </id>
       <receiverHook> 0 </receiverHook>
       <isContract> ISCONTRACT </isContract>
       <acceptRegularNoReceiverHook> AcceptRegular </acceptRegularNoReceiverHook>
```
Yet there is some extra functionality for the case receiver hook is not implemented.
When receiver address is not a contract address, or the configuration does not support
regular addresses with no receiver hook, the operation reverts.

#### 3.7.6 #transfer
This construct performs the actual balance transfer. There are 2 rules. First is for case 
sender and recipient are different, when actual balance changes happen.
The second rule is for the case when sender and recipient are the same. In this case, 
as expected, balance remains unchanged.

There is no need for side conditions checking for operation validity, because validity
was already checked by `#ensures` construct generated by the rule
for `operationSend`.

```
  syntax Stmt ::= #transfer ( Address , Address , ValueType )

  rule <k> #transfer ( From , To , Value ) => . ... </k>
       <account>
         <id> From </id>
         <balance> ValueFrom => ValueFrom -Int Value </balance>
         ...
       </account>
       <account>
         <id> To </id>
         <balance> ValueTo => ValueTo +Int Value </balance>
         ...
       </account>
       requires From =/=Int To

  rule <k> #transfer ( From , To , Value ) => . ... </k>
       requires From ==Int To
```

#### 3.7.7 #isMultipleOf
Another functionality required to define `operationSend` is the construct, 
`#isMultipleOf`, which checks whether the amount involved in an operation
is a multiple of global granularity.
```
  syntax BExp ::= #isMultipleOf ( AExp , AExp ) [seqstrict]

  rule #isMultipleOf ( Value:Int , Granularity:Int ) => Value /Int Granularity *Int Granularity ==Int Value
```

#### 3.7.8 Logging
The last auxiliary constructs used by all state-altering operations in ERC777-K are 
related to logging. The construct `#log(Event)` is used for events defined in ERC777.
It simply appends the event to the `<log>` cell.

The construct `#logERC20` is used for ERC20-specific events. It logs the given event 
only when ERC20 compatibility is enabled.

```
  syntax Stmt ::= #log ( Event )
                | #logERC20 ( Event )

  rule <k> #log(Log) => . ... </k>
       <log> Logs => Logs Log </log>

  rule <k> #logERC20(Log) => . ... </k>
       <log> Logs => Logs Log </log>
       <ERC20CompatibleC> ERC20Compatible </ERC20CompatibleC>
       requires ERC20Compatible

  rule <k> #logERC20(Log) => . ... </k>
       <ERC20CompatibleC> ERC20Compatible </ERC20CompatibleC>
       requires notBool ERC20Compatible
```


#### 3.8. Burn operations
These are defined in similar manner to `send` and `operatorSend`. They reuse a great deal
of auxiliary constructs. 

Wording from standard: contract MAY prevent some/all holders from burning for any reason
vs Tokens MAY be burned from any holder address.

We implement this through the cell `<burnAllowed>`. If it is true, burn operation 
will proceed according to other rules.
If it is false, burn operation is not allowed for the entire contract, and any 
burn operation will revert.

```
  rule <k>         burn(        Amount:Int   )
        => operatorBurn(Caller, Amount,    "") ... </k>
       <caller> Caller </caller>

  rule <k> operatorBurn(0 => Caller, _,_) ... </k>
       <caller> Caller </caller>
    requires Caller =/=Int 0 //just for safety

  rule <k> operatorBurn(Holder:Int, Amount:Int, OperatorData)
        => #atomicBegin
        ~> #ensure(#and(isOperatorFor(Operator, Holder),
                        #isMultipleOf(Amount, granularity()),
                        Amount >=Int 0,
                        Balance >=Int Amount,
                        Holder =/=Int 0,
                        BurnAllowed))
        ~> #callSender(Operator, Holder, 0, Amount, "", OperatorData)
        ~> #burn(Holder, Amount)
        ~> #log(Burned(Operator, Holder, Amount, OperatorData))
        ~> #logERC20(Transfer(Holder, 0, Amount)) // ERC20
        ~> #atomicEnd(#void) ... </k>
       <caller> Operator </caller>
       <id> Holder </id>
       <balance> Balance </balance>
       <burnAllowed> BurnAllowed </burnAllowed>
    requires Holder =/=Int 0

  syntax Stmt ::= #burn ( Address , ValueType )

  rule <k> #burn(Holder, Amount) => . ... </k>
       <account>
         <id> Holder </id>
         <balance> Balance => Balance -Int Amount </balance>
         ...
       </account>
       <supply> Supply => Supply -Int Amount </supply>
       requires Balance >=Int Amount andBool Supply >=Int Amount
```

Calling `operatorBurn` with 1st argument 0 is allowed,
in this case `Holder` will be the caller. If holder has a sender hook registered,
it is called before the operation, by using the already defined `#callSender`.
Actual balance decrease is performed by `#burn`. Here, in addition to decreasing the 
balance, we also decrease the `<supply>`, since the total supply also decreases.

Burn operation uses the same mechanism as send for transaction atomicity, enforcing
preconditions and logging. The ERC20 `Transfer` event has the `To` argument equal to 0.
to represent burning.

### 3.9. operatorMint
ERC777 does not define specific functions to mint tokens. Instead,
it specifies several rules that MUST be respected when minting tokens.
The definition of `mint` below is a minimal example of minting function
that conforms to those rules. We define the function `operationMint`
which must be called by an operator, on behalf of a recipient. It follows the same 
structure as `operatorSend` and `operatorBurn`.

This function is not allowed to have argument `0`, in this case it results in `revert`.
The relevant hook for mint operation is `tokensReceived`.
When hook is not defined, this semantics allows all addresses to receive tokens through 
minting.

```
  rule operatorMint(0, _,_,_) => revert

  rule <k> operatorMint(Holder, Amount, Data, OperatorData)
        => #atomicBegin
        ~> #ensure(#and(Amount >=Int 0,
                        Supply +Int Amount <=Int #MAXVALUE,
                        #isMultipleOf(Amount, granularity()),
                        Holder =/=Int 0,
                        isOperatorFor( Operator , Holder )))
        ~> #mint(Holder, Amount)
        ~> #callRecipient(Operator, 0, Holder, Amount, Data,  OperatorData, false)
        ~> #log(Minted(Operator, Holder, Amount, Data, OperatorData))
        ~> #logERC20(Transfer(0, Holder, Amount))
        ~> #atomicEnd(#void) ... </k>
       <caller> Operator </caller>
       <id> Holder </id>
       <supply> Supply </supply>
    requires Holder =/=Int 0

  syntax Stmt ::= #mint ( Address , ValueType )

  rule <k> #mint(Holder, Amount) => . ... </k>
       <account>
         <id> Holder </id>
         <balance> Balance => Balance +Int Amount </balance>
         ...
       </account>
       <supply> Supply => Supply +Int Amount </supply>
```

### 3.10. ERC20 functions
In this section we have the rules for ERC20 functions. There several differences comapared to
same rules in ERC20-K. First: `transfer` operation will log 2 events: the ERC20 `Transfer`
and the ERC777 `Sent`. Second difference, we use `#atomicBegin ... #atomicEnd`
transaction mechanism, to rollback the transaction if it ends in revert.
Third, the `transfer` operation calls the sender and recipient hooks, as required
by ERC777 standard.

Another, more subtle difference is the return type. ERC777 state-changing functions
ton't return a value, while ERC20 functions should return boolean value `true`. 
This return value is represented by the argument of `#atomicEnd`, which we define later.
```
  rule <k> allowance(Owner:Int, Spender:Int) => Allowance ...</k>
       <id> Owner </id>
       <spender> Spender </spender>
       <amount> Allowance </amount>
```

RC20 compatibility requirement: The decimals of the token MUST always be 18.
```
  rule <k> decimals() => 18 </k>
```

```
  rule <k> approve(Spender:Int, Allowance:Int) => true ...</k>
       <caller> Owner </caller>
       <id> Owner </id>
       <spender> Spender </spender>
       <amount> _ => Allowance </amount>
       <log> Log => Log Approval(Owner, Spender, Allowance) </log>
       requires Allowance >=Int 0

  rule <k> approve(Spender:Int, Allowance:Int) => revert ...</k>
       <caller> Owner </caller>
       <id> Owner </id>
       <spender> Spender </spender>
       <amount> _ => Allowance </amount>
       requires Allowance <Int 0

  rule <k> transfer(To:Int, Value:Int) => #transferAux(To, Value, balanceOf(From), balanceOf(To)) ...</k>
       <caller> From </caller>

  syntax K ::= "#transferAux" "(" Address "," ValueType "," AExp "," AExp ")" [seqstrict(3,4)]
  rule <k> #transferAux(To:Int, Value:Int, BalanceFrom:Int, BalanceTo:Int)
        => #atomicBegin
        ~> #ensure(#and(Value >=Int 0,
                        BalanceFrom >=Int Value,
                        #isMultipleOf(Value, granularity()),
                        #or(From ==Int To, BalanceTo +Int Value <=Int #MAXVALUE)))
        ~> #callSender(From, From, To, Value, "", "")
        ~> #transfer(From, To, Value)
        ~> #callRecipient(From, From, To, Value, "", "", true) // should accept even if To isn't registered and To is contract )
        ~> #log(Transfer(From, To, Value))
        ~> #log(Sent(From, From, To, Value, "", ""))
        ~> #atomicEnd(true) ... </k>
       <caller> From </caller>

  rule transferFrom(From:Int, To:Int, Value:Int)
       => #transferFromAux(From, To, Value, balanceOf(From), balanceOf(To))

  syntax K ::= "#transferFromAux" "(" Address "," Address "," ValueType "," AExp "," AExp ")" [seqstrict(4,5)]
  rule <k> #transferFromAux(From, To, Value, BalanceFrom:Int, BalanceTo:Int)
        => #atomicBegin
        ~> #ensure(#and(Value >=Int 0,
                        BalanceFrom >=Int Value,
                        #isMultipleOf(Value, granularity()),
                        #or(From ==Int To, BalanceTo +Int Value <=Int #MAXVALUE),
                        Allowed >=Int Value))
        ~> #callSender(Operator, From, To, Value, "", "")
        ~> #transferWithAllowance(Operator, From, To, Value)
        ~> #callRecipient(Operator, From, To, Value, "", "", true)
        ~> #log(Transfer(From, To, Value))
        ~> #log(Sent(From, From, To, Value, "", ""))
        ~> #atomicEnd(true) ... </k>
       <caller> Operator </caller>
       <id> From </id>
       <spender> Operator </spender>
       <amount> Allowed </amount>

  syntax Stmt ::= #transferWithAllowance ( Address , Address , Address , ValueType )

  rule <k> #transferWithAllowance(Operator, From, To, Value) => . ... </k>
       <account>
         <id> From </id>
         <balance> BalanceFrom => BalanceFrom -Int Value </balance>
         <spender> Operator </spender>
         <amount> Allowed => Allowed -Int Value </amount>
         ...
       </account>
       <account>
         <id> To </id>
         <balance> BalanceTo => BalanceTo +Int Value </balance>
         ...
       </account>
       requires From =/=Int To

  rule <k> #transferWithAllowance(Operator, From, To, Value) => . ... </k>
       <account>
         <id> From </id>
         <balance> Balance </balance>
         <spender> Operator </spender>
         <amount> Allowed => Allowed -Int Value </amount>
         ...
       </account>
       requires From ==Int To
```

### 3.11. #atomicBegin, #atomicEnd

The construct `#atomicBegin` is the first operation performed by `operatoSend`, 
`operatorBurn`, `operatorMint`:
```
  syntax KItem ::= "#atomicBegin"
                 | "#atomicEnd" "(" K /*output*/ ")"
                 | #snapshot ( K , EventLog , ValueType )

  rule <k> #atomicBegin => . ... </k>
       <accounts> Accounts </accounts>
       <log> Log </log>
       <supply> Supply </supply>
       <atomic> .K => #snapshot ( Accounts , Log , Supply ) </atomic>
```

It stores the content of cells `<accounts>, <log>, <supply>` into `<atomic>`,
to be used in case a `revert` operation happens.
Note that this semantics allows only one functional frame to be stored, and does 
not support a function stack. This is enough to capture the essence of ERC777.

```
  rule <k> #atomicEnd(OUT:K) => OUT ... </k>
       <atomic> _ => .K </atomic>
```
The construct `#atomicEnd` marks the end of a function. If this construct is reached, 
it means that function was executed successfully. Then the content of `<atomic>` 
is deleted, because its scope is the function scope. In addition, the `#atomicEnd`
is rewritten into its argument, which contains the return value.
This value is `true` for functions that return `true` in case of success (ERC20),
of `#void` for functions that don't have return value (ERC777).

### 3.12. revert

We have 2 rules for `revert`:
```
  rule <k> revert ~> (KI:KItem => .K) ... </k>
       <atomic> #snapshot ( _ , _ , _ ) </atomic>
  requires (KI =/=K #atomicEnd(#void)) andBool (KI =/=K #atomicEnd(true))

  rule <k> revert ~> (#atomicEnd(_) => .K) ... </k>
       <accounts> _ => Accounts </accounts>
       <log> _ => Log </log>
       <supply> _ => Supply </supply>
       <atomic> #snapshot ( Accounts , Log , Supply ) => .K </atomic>
```
Both rules match only in the case when `<atomic>` is not empty, e.g. when we are inside
an ERC777, or ERC20 function.
The first rule deletes any K item after `revert`, except `atomicEnd`, the only 
item with which revert can interact.
The 2nd rule is matches when revert is followed by `#atomicEnd`. It restores
the state stored in `<atomic>` and keeps `revert` at the top of computation.
Thus, once triggered, `revert` is always propagated outside the function that generated it.
In module `ERC777-IMP` there are additional rules that print a message to the output
when `revert` is encountered, and allowthe computation to continue.


### 3.13. Utility constructs
The last section of `ERC777` module contans a collection of K utility functions.
Here we have `#ensure` that evaluates the the preconditions of a function.
If the argument evaluates to true, evalaution continues. Otherwise evaluation results in 
`revert`.

Then `#ite` represents a general-purpose conditional operator with support for strictness.
Similarly, `#and` and `#or` operators implement boolean expressions with strictness,
to be used as arguments to `#ensures`.
```

  syntax Stmt ::= #ensure ( BExp ) [strict]

  rule #ensure ( B:Bool ) => .       requires B
  rule #ensure ( B:Bool ) => revert  requires notBool B

  syntax K ::= #ite ( BExp , K , K ) [strict(1)]

  rule #ite ( B:Bool , T , F ) => T  requires B
  rule #ite ( B:Bool , T , F ) => F  requires notBool B

  syntax BExp ::= "#and" "(" BExp "," BExp ")"                                     [strict]
                | "#and" "(" BExp "," BExp "," BExp ")"                            [strict]
                | "#and" "(" BExp "," BExp "," BExp "," BExp ")"                   [strict]
                | "#and" "(" BExp "," BExp "," BExp "," BExp "," BExp ")"          [strict]
                | "#and" "(" BExp "," BExp "," BExp "," BExp "," BExp "," BExp ")" [strict]
                | "#and" "(" BExp "," BExp "," BExp "," BExp "," BExp "," BExp "," BExp ")" [strict]
                | "#or"  "(" BExp "," BExp ")"                                     [strict]

  rule #and ( A:Bool , B:Bool )                                     => A andBool B
  rule #and ( A:Bool , B:Bool , C:Bool )                            => A andBool B andBool C
  rule #and ( A:Bool , B:Bool , C:Bool , D:Bool )                   => A andBool B andBool C andBool D
  rule #and ( A:Bool , B:Bool , C:Bool , D:Bool , E:Bool )          => A andBool B andBool C andBool D andBool E
  rule #and ( A:Bool , B:Bool , C:Bool , D:Bool , E:Bool , F:Bool ) => A andBool B andBool C andBool D andBool E andBool F
  rule #and ( A:Bool , B:Bool , C:Bool , D:Bool , E:Bool , F:Bool , G:Bool )
       => A andBool B andBool C andBool D andBool E andBool F andBool G
  rule #or  ( A:Bool , B:Bool )

endmodule

```

## 4 How To Use ERC777-K

One way to use ERC777-K, illustrated in module `ERC777-IMP` and [tests](tests), 
is to import it in
other programming language semantics and thus offer ERC777 support to those
languages.
In particular, this can be useful to test the ERC777-K specification
programmatically, as well as for producing tests that can be then used with
implementations of ERC.

Another way to use ERC20-K is as a standard for ERC777 compliance.
That is, as an answer to *what* needs to be proved about a smart contract
claiming to implement an ERC777 token.
Each of the rules above is one reachability claim that needs to be proved.

See
[ERC20-K](https://github.com/runtimeverification/erc20-semantics) project for a similar
semantics for ERC20 standard, and more explanations.
